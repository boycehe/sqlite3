! --------------------------------------------------------------------- !
!  FACILITY:	SQLITE3							!
!									!
!  ABSTRACT:	Builds the symbol vector for the SQLite3 shareable	!
!		image.							!
!									!
!  MODULE DESCRIPTION:							!
!									!
!	This module contains the code that builds the symbol vector	!
!  for SQLITE3.  It does this by reading in an existing symbol table,	!
!  then the symbols defined in the object library.  Any new symbols are !
!  tacked on the end and the new symbol vector is saved.  If any symbols!
!  are not present in the object library that are listed in the symbol  !
!  vectore, are warning is issued.					!
!									!
!	   ** Note - this macro must be squished before use **		!
!									!
!  AUTHOR:	Tim Sneddon.						!
!									!
!  Copyright (c) 2013, Endless Software Solutions.			!
!									!
!  All rights reserved.							!
!									!
!  Redistribution and use in source and binary forms, with or without	!
!  modification, are permitted provided that the following conditions	!
!  are met:								!
!									!
!      * Redistributions of source code must retain the above		!
!        copyright notice, this list of conditions and the following	!
!        disclaimer.							!
!      * Redistributions in binary form must reproduce the above	!
!        copyright notice, this list of conditions and the following	!
!        disclaimer in the documentation and/or other materials provided!
!        with the distribution.						!
!      * Neither the name of the copyright owner nor the names of any	!
!        other contributors may be used to endorse or promote products	!
!        derived from this software without specific prior written	!
!        permission.							!
!									!
!  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS	!
!  "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT	!
!  LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR!
!  A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT !
!  OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,!
!  SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT	!
!  LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,!
!  DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY!
!  THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT  !
!  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE!
!  OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE. !
!									!
!  CREATION DATE:  9-MAR-2013						!
!									!
!  MODIFICATION HISTORY:						!
!									!
!      09-MAR-2013 V1.0    Sneddon     Initial coding.			!
! --------------------------------------------------------------------- !

@^A%MAKE_SYMBOL_VECTOR  V1.0%	! Identify myself...			!
13^T 10^T

0J Z"N <@FS%^ES% %;>		! Clean up the buffer, if it's there	!
	0J 0A-32"E D '		!   strip leading spaces		!
	ZJ -1A-32"E -D '	!   and trailing spaces			!
'				! endif buffer there			!
HXZ HK				! Save possible command			!

!		Set flags for our use, and close this file		!

[S ^XUS [S EDUS [S EHUS [S	! Save all the existing flags		!
ESUS [S EVUS [S ETUS [S EUUS [S	! so we can restore the user to		!
				! his original state on exit		!

^D				! Set Radix to 10			!
0^X				! Case-sensitive search			!
0ED				! ^ in search means ConTRL		!
				! Disable Y or _ if data loss		!
2EH				! Full error messages			!
0ES 0EV				! No typeout after search		!
2#16#128#512#8192ET
! Set ET flag as follows -						!
!     1 rw typeout in image mode					!
!     2 rw terminal is a scope						!
!     4 rw read lower case						!
!     8 rw ^T reads with no echo					!
!    16 rw cancel ^O on typeout						!
!    32 rw read with no wait, returns -1 if no input			!
!    64 rw detach flag							!
!   128 rw abort on error						!
!   256 rw truncate to set width					!
!   512 ro terminal is scope and watch is present			!
!  1024 ro terminal is refresh and watch is present			!
!  2048 xx not used							!
!  4096 rw 1 => 8bit, 0 => 7bit						!
!  8192 rw Surrogate escape on						!
! 16384 xx not used							!
! 32768 rw trap ^C and reset						!
-1EU				! No case flagging on output		!
!  EO  is Teco's Version Number						!
! 0EJ  is Job Number							!
! 1EJ  is KB Number							!
! 2EJ  is PPN								!
@EI%%				! Close this file			!

! --------------------------------------------------------------------- !
! *									!
!									!
! X$ is library listing file name (input)	!
! Y$ is symbol vector file name (input/output)	!
! N$ is new symbols from library listing	!
! T$ is symbol table !
! H$ contains SQLITE_API prefixed stuff from sqlite3.h header file !
! !
! -- keep moving everything into mainline...arch-specific is only macros!
! A$ contains macro to generate Alpha/I64 symbol vector !
! L$ contains macro to build symbol vector	!
! R$ contains macro to load library listing	!
! V$ contains macro to generate VAX vector	!
!!
! --------------------------------------------------------------------- !

:QZ"N					! If something given on command	!
    HK GZ HXY HK			!   then use it (put it in .O$)	!
'					! endif				!
GY 0J Z"N <@FS%^ES% %;>			! Clean up the buffer, if there	!
    0J 0A-32"E D '			!   strip leading spaces	!
    ZJ -1A-32"E -D '			!   and trailing spaces		!
    0J @:S%=%"S				!   search '=', if found	!
	0,.+^SXY			!     then store output in .O$	!
	.,ZXX				!     store input in .I$	!
    '					!   end if found '='		!
'					! endif buffer there		!

! --------------------------------------------------------------------- !
! Read in the library listing and convert to a ^ delimited list of	!
! global symbols, all stored in N$.					!
! --------------------------------------------------------------------- !
@^A%...Reading symbol vector table from library listing...%
HK					! Clear the buffer		!
@^UN%%					! Clear input symbol list	!
:@ER%^EQX%"F                            ! If could not open file then   !
    @^A%Can't find file "% :G.I @^A%"...% 13^T 2<10^T>
|					! endif could not open file	!
    0U0					! Clear "in module"		!
    < :EY;				! Yank in a page		!
    	<
	    ::@S%Module %"S		! If line starts Module		!
	        -1U0			!   then set "in module"	!
	    | Q0"S			! elseIf "in module"		!
	    	<
		    ::@S%^ES%		!   skip leading whitespace	!
		    .U1			!   save start 			!
		    @:FB% %"S		!   look for next sep, if found	!
			.+^SU2		!     save end			!
		    | @:FB%^EL%"S	!   elseIf at end of line	!
			.-2U2		!     save end (minus eol)	!
		    	Q1-Q2"E		!     If line is empty		!
			    0U0		!       clear "in module"	!
			    0;		!	leave the loop		!
		    	'		!     endIf			!
		    ' '			! endIf				!
		    .UN
		    Q1,Q2:XN ^^,:@^UN%%
	    	>
	    ' '				! endIf				!
    	L .-Z;>				! Advance line, leave if none	!
    >
    HK					! clear text buffer		!
    @^A%done...% 13^T 10^T
'

! --------------------------------------------------------------------- !
! Here we load all lines from the header file that describes the public	!
! interface to SQLite3, sqlite3.h, and store it away in H$.  This is	!
! used to verify the global symbols read from the library listing are	!
! in fact public routines (and data cells).				!
! --------------------------------------------------------------------- !
:QN"N					! if lib listing loaded	then	!
    @^A%...Loading public header file for comparison...%
    @^UH%%				!   Clear header file buffer	!
    :@ER%SRC_DIR:SQLITE3.H%"S		!   Open public, if success	!
    	< :EY;				!     then Yank in a page	!
	    < :@S%^ELSQLITE_API%;	!       grab SQLITE_API lines	!
	    	:XH			!	  stick them in H$	!
	    >				!	end loop		!
    	>				!     end loop			!
    	@^A%done...% 13^T 10^T		!     report			!
    |					!   else			!
	@^A%unable to open SRC_DIR:SQLITE3.H% 13^T 10^T
    '					!   endif			!
'					! endif				!

! --------------------------------------------------------------------- !
! Load the existing symbol table.  We use this to test for missing	!
! entries (useful when building a new version of SQLite) as well as	!
! retaining a record of the symbol table, so it can be updated without	!
! losing compatability.							!
! --------------------------------------------------------------------- !
:QN+:QH"N
    @^A%...Loading existing symbol vector table...%
    HK					! Clear text buffer		!
    :@ER%^EQY%"S			! If can open existing vector	!
	:EY				!   then load it into buffer	!
    '					! endif				!
    0J					! reset .			!
    @^A%done...% 13^T 10^T

! --------------------------------------------------------------------- !
! Using the public interface definitions, previous symbol vector table	!
! and the new symbols from the object library listing, we construct an	!
! udpated table.							!
! --------------------------------------------------------------------- !
    @^A%...Building new symbol vector table...%
    0U.P <				! Init pointer			!
	@^U.S%%
	<
	    Q.PQN:@^U.S%%
	    %.PQN-^^,"E
		%.P^[
		0;
	    '
	>
	0J :@S%^EQ.S,^ED,^EA^X^EL%"F	!   if no matching record	!
	    HX2 .U2			!     store sym table		!
	    HK GH			!     load public interface	!
	    0J :@S%^EQ.S%U3 Q3"S	!     if public			!
		::@S%^ES%^[		!       skip whitespace		!
		0AU3			!       store next character	!
	    '				!     end if			!
	    HK G2 Q2J			!     restore sym table		!
	    Q3"N			!     if public			!
		ZJ G.S @I%,2,%		!       then new record		!
	        Q3-^^("E		!       if function		!
		    @I%P%		!	  set symbol type to P	!
		|			!	else treat as data	!
		    @I%D%		!         set symbol type to D	!
		'			!	end if			!
		13@I%% 10@I%%		!	terminate record	!
	    '				!     end if			!
	|				!   else			!
	    -2@S%,% C 			!     backup to flag		!
	    D @I%1%			!     set it to known		!
    	'				!   end if			!
    :QN-Q.P"E 0;' >
    @^A%...done...% 13^T 10^T

! --------------------------------------------------------------------- !
! Now, examine the flags attached to the symbols to determine if there	!
! have been any changes to the symbol vector table and reports them.	!
! --------------------------------------------------------------------- !
    @^A%...Checking for changes in symbol vector table...%
    -1UT				! Symbol table unchanged...	!
    HXT					! Store symbol table		!
    0J
    <
	@^A%.%
	@^U.S%%
	< 0A:@^U.S%% C 0A-^^,"E 0;' >	! fetch name into .S$ !
	@S%,% \U.S			! fetch flag !
	-D @I%0%			!  clear flag !
	Q.S-1"L				! if not found !
	    @S%,% 0A-^^S"N		!   if not already aware of it !
	        13^T 10^T @^A%   ...% :G.S @^A% is missing%
		D @I%S%			!    change so we are !
	        0UT			!    indicate a change !
	    '				!   end if !
	| Q.S-1"G			! else if new symbol !
	    13^T 10^T @^A%   ...% :G.S @^A% is new%
	    0UT				!    indicate a change !
	' '				! end if !

	L
    .-Z; >
    13^T 10^T
    QT"F
	@^A%...symbol table has changed%
	:@EW%^EQY%"S
	    HP EF
	|
	    @^A%Could not open % :GY @^A% for output% 13^T 10^T
	'
    |
	@^A%...symbol table has not changed%
    '
    @^A%...done...% 13^T 10^T
!

Change the order of the flags...to name,flag,type
this can be used to improve the code above...need to set that type to
S if we find something that isn't there

After we have written out the symbol vector...re-write the file with
all flags to 0.

only write out the symbol vector if there is a change...
only write out the machine specific one if there is a change...

MAKE_SYMBOL_VECTOR SRC_DIR:SYMBOL_VECTOR.TXT=ETC_DIR:SQLITE3.LIS SRC_DIR:SQLITE3_VECTOR.MAR

!

! need to update the table so everything is 0 again... 

    then dump it out... !

    @^A%...Generating VAX shareable image vector...%
    HXT HK				! Save vector info		!
    @I%
	.TITLE	SQLITE3_SHR_VECTOR

;
; This is automatically generated by MAKE_SYMBOL_VECTOR. DO NOT EDIT!!!
;

	.MACRO	ROUTINE NAME
	.EXTRN		NAME
	.ALIGN	QUAD
	.TRANSFER	NAME
	.MASK		NAME
	JMP		NAME+2
	.ENDM

	.PSECT	___SQLITE3_SHR_VECTOR -
			PIC,USR,CON,REL,LCL,SHR,EXE,RD,NOWRT,QUAD

%

    @I%
	.END
%

    @^A%done...% 13^T 10^T

!	    @^A%New symbol % :G.S @^A% found...% 13^T 10^T!
! Need to determine architecture !
! Symbol table file should be P2...SYMBOL_TABLE.TXT=NEW.TXT SYMBOL_VECTOR.MAR!

!    [.V MV!
    ! process to build vector...mention new symbols and gone symbols !
'

! --------------------------------------------------------------------- !
! *		    Now restore all the flags and exit			!
! --------------------------------------------------------------------- !

]S QSEU ]S QSET ]S QSEV ]S QSES ]S	! Restore all the flags we	!
QSEH ]S QSED ]S QS^X ]S			! saved at the start		!

^C

